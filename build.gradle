/*
 *  /***************************************************************************
 *  Copyright (c) 2017, EPAM SYSTEMS INC
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *     http://www.apache.org/licenses/LICENSE-2.0
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 *  ***************************************************************************
 */
group 'com.epam.dep.esp'
version version

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
archivesBaseName = rootProject.name

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/groups/public"
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: nexusUsername, password: nexusPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: nexusUsername, password: nexusPassword)
            }

            pom.project {
                packaging 'jar'
                name rootProject.name
                description 'Groovy ManiPulator'
                url 'https://github.com/epam/GMP'

                scm {
                    url 'https://github.com/epam/GMP'
                    connection 'scm:https://github.com/epam/GMP.git'
                    developerConnection 'scm:https://github.com/epam/GMP.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'asamuta'
                        name 'Andrei Samuta'
                        email 'a.samuta@gmail.com'
                    }
                }
            }
        }
    }
}

dependencies {
    compile "org.codehaus.groovy:groovy:$groovyVersion"
    compile "org.springframework:spring-context-support:$springVersion"
    compile "org.springframework:spring-aspects:${springVersion}"
    compile "org.slf4j:slf4j-log4j12:${slf4jVersion}"
    compile "com.epam.dep.esp:gmp-common:$gmpCommonVersion"
    compile "commons-cli:commons-cli:$commonsCliVersion"
    compile "com.google.guava:guava:$guavaVersion"
    compile "org.yaml:snakeyaml:$snakeyamlVersion"
//TEST
    testCompile group: 'junit', name: 'junit', version: junitVersion
    testCompile "org.springframework:spring-test:${springVersion}"

}

task copyConfig(type: Copy) {
    into "$buildDir/install"
    from "src/main/resources/gmp.properties"
}

task copyBinary(type: Copy) {
    into "$buildDir/install"
    from "$buildDir/libs"
}

task copyDepandancies(type: Copy) {
    into "$buildDir/install/lib"
    from configurations.runtime
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Groovy ManiPulator',
                'Implementation-Version': version,
                'Implementation-Vendor': 'EPAM SYSTEMS INC',
                'Main-Class': 'com.epam.gmp.ChainExecutor',
                'Class-Path': configurations.compile.collect { 'lib/' + it.getName() }.join(' ')
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

if (project.hasProperty('signing.keyId')) {
    signing {
        sign configurations.archives
    }
}

test {
    testLogging {
        showStandardStreams = true
    }
}